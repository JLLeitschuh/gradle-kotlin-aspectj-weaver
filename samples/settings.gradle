include ":java-only"

includeBuild("../plugin")

void configureBuildFileName(ProjectDescriptor project) {
    /*
     * Instead of every file being named build.gradle.kts we instead use the name ${project.name}.gradle.kts.
     * This is much nicer for searching for the file in your IDE.
     */
    final String groovyName = "${project.name}.gradle"
    final String kotlinName = "${project.name}.gradle.kts"
    project.buildFileName = groovyName
    if (!project.buildFile.isFile()) {
        project.buildFileName = kotlinName
    }
    assert project.buildFile.isFile() : "File named $groovyName or $kotlinName must exist."
    project.children.each { configureBuildFileName(it) }
}
configureBuildFileName(rootProject)


